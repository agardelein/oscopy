## ipython3.m4 - Check for Ipython3 version on a system. -*-Autoconf-*-
## Copyright (C) 2013 Arnaud Gardelein.
## Author: Arnaud Gardelein <arnaud@oscopy.org>
##
## This program is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 2 of the License, or
## (at your option) any later version.
##
## This program is distributed in the hope that it will be useful, but
## WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
## General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program; if not, write to the Free Software
## Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
##
## As a special exception to the GNU General Public License, if you
## distribute this file as part of a program that contains a
## configuration script generated by Autoconf, you may include it under
## the same distribution terms that you use for the rest of that program.

# AM_PATH_IPYTHON3([MINIMUM-VERSION], [ACTION-IF-FOUND], [ACTION-IF-NOT-FOUND])
# ---------------------------------------------------------------------------
# Adds support for distributing IPYTHON3 modules and packages. 
#
# If the MINIMUM-VERSION argument is passed, AM_PATH_IPYTHON3 will
# cause an error if the version of IPYTHON3 installed on the system
# doesn't meet the requirement.  MINIMUM-VERSION should consist of
# numbers and dots only.

# check also matplotlib, numpy, dbus, xdg, gtk
AC_DEFUN([AM_PATH_IPYTHON3],
 [
  dnl Find a IPYTHON3 interpreter.
  m4_define_default([_AM_IPYTHON3_INTERPRETER_LIST],
[ipython3])

  AC_ARG_VAR([IPYTHON3], [the Ipython3 interpreter])

  m4_if([$1],[],[
    dnl No version check is needed.
    # Find any Ipython3 interpreter.
    if test -z "$IPYTHON3"; then
      AC_PATH_PROGS([IPYTHON3], _AM_IPYTHON3_INTERPRETER_LIST, :)
    fi
    am_display_IPYTHON3=Ipython3
  ], [
    dnl A version check is needed.
    if test -n "$IPYTHON3"; then
      # If the user set $IPYTHON3, use it and don't search something else.
      AC_MSG_CHECKING([whether $IPYTHON3 version >= $1])
      AM_IPYTHON3_CHECK_VERSION([$IPYTHON3], [$1],
			      [AC_MSG_RESULT(yes)],
			      [AC_MSG_ERROR(too old)])
      am_display_IPYTHON3=$IPYTHON3
    else
      # Otherwise, try each interpreter until we find one that satisfies
      # VERSION.
      AC_CACHE_CHECK([for a Ipython3 interpreter with version >= $1],
	[am_cv_pathless_IPYTHON3],[
	for am_cv_pathless_IPYTHON3 in _AM_IPYTHON3_INTERPRETER_LIST none; do
	  test "$am_cv_pathless_IPYTHON3" = none && break
	  AM_IPYTHON3_CHECK_VERSION([$am_cv_pathless_IPYTHON3], [$1], [break])
	done])
      # Set $IPYTHON3 to the absolute path of $am_cv_pathless_IPYTHON3.
      if test "$am_cv_pathless_IPYTHON3" = none; then
	IPYTHON3=:
      else
        AC_PATH_PROG([IPYTHON3], [$am_cv_pathless_IPYTHON3])
      fi
      am_display_IPYTHON3=$am_cv_pathless_IPYTHON3
    fi
  ])

  if test "$IPYTHON3" = :; then
  dnl Run any user-specified action, or abort.
    m4_default([$3], [AC_MSG_ERROR([no suitable Ipython3 interpreter found])])
  else

  dnl Query IPYTHON3 for its version number.

  AC_CACHE_CHECK([for $am_display_IPYTHON3 version], [am_cv_ipython3_version],
    [am_cv_ipython3_version=`$IPYTHON3 --colors=NoColor -c "import IPython,sys; sys.stdout.write(IPython.release.version if hasattr(IPython, 'release') else IPython.Release.version)"`])
  AC_SUBST([IPYTHON3_VERSION], [$am_cv_ipython3_version])

  dnl Use the values of $prefix and $exec_prefix for the corresponding
  dnl values of IPYTHON3_PREFIX and IPYTHON3_EXEC_PREFIX.  These are made
  dnl distinct variables so they can be overridden if need be.  However,
  dnl general consensus is that you shouldn't need this ability.

  dnl AC_SUBST([IPYTHON3_PREFIX], ['${prefix}'])
  dnl AC_SUBST([IPYTHON3_EXEC_PREFIX], ['${exec_prefix}'])

  dnl At times (like when building shared libraries) you may want
  dnl to know which OS platform IPYTHON3 thinks this is.

  dnl AC_CACHE_CHECK([for $am_display_IPYTHON3 platform], [am_cv_IPYTHON3_platform],
  dnl  [am_cv_IPYTHON3_platform=`$IPYTHON3 -c "import sys; sys.stdout.write(sys.platform)"`])
  dnl AC_SUBST([IPYTHON3_PLATFORM], [$am_cv_IPYTHON3_platform])
  dnl Run any user-specified action.
  $2
  fi
])

# AM_IPYTHON3_CHECK_VERSION(PROG, VERSION, [ACTION-IF-TRUE], [ACTION-IF-FALSE])
# ---------------------------------------------------------------------------
# Run ACTION-IF-TRUE if the IPYTHON3 interpreter PROG has version >= VERSION.
# Run ACTION-IF-FALSE otherwise.
AC_DEFUN([AM_IPYTHON3_CHECK_VERSION],
 [prog="import sys, IPython;minver = list(map(int, '$2'.split('.'))) + [[0, 0, 0]];ver = list(map(int, (IPython.release.version if hasattr(IPython, 'release') else IPython.Release.version).split('.')[[0:3]])) + [[0, 0, 0]];minverhex = sum([[minver[i]<<((4-i)*8) for i in range(0, 4)]]);verhex = sum([[ver[i]<<((4-i)*8) for i in range(0, 4)]]);sys.stdout.write('1' if verhex < minverhex else '0');"
  AS_IF([AM_RUN_LOG_IPYTHON3([$1 --colors=NoColor -c "$prog"])], [$3], [$4])])

# AM_RUN_LOG_IPYTHON3(COMMAND)
# -------------------
# Run COMMAND, save the output in ac_status, and log it.
# (This has been adapted from Python's AM_RUN_LOG macro.)
AC_DEFUN([AM_RUN_LOG_IPYTHON3],
[{ echo "$as_me:$LINENO: $1" >&AS_MESSAGE_LOG_FD
#   ($1) >&AS_MESSAGE_LOG_FD 2>&AS_MESSAGE_LOG_FD
    # First check whether the program exists
    # Probably /dev/null is not portable ouside linux systems...
   which `echo $1 | cut -d ' ' -f 1` > /dev/null
   if test "$?" = 1; then
      ac_status=1
      (exit $ac_status);
   else
      ac_status=`$* | cut -c 1`
      echo "$as_me:$LINENO: \$? = $ac_status" >&AS_MESSAGE_LOG_FD
      if test $ac_status = "1"; then
      	 (exit 1);
      else
         (exit 0);
      fi
   fi
 }])
